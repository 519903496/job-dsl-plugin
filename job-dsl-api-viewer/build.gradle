import de.undercouch.gradle.tasks.download.Download

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:${assetPipelineVersion}"
        classpath "com.bertramlabs.plugins:less-asset-pipeline:${assetPipelineVersion}"
        classpath "com.bertramlabs.plugins:handlebars-asset-pipeline:${assetPipelineVersion}"
    }
}

plugins {
    id 'com.eriwen.gradle.css' version '2.14.0'
    id 'com.eriwen.gradle.js' version '2.14.1'
}

apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'com.bertramlabs.asset-pipeline'

repositories {
    maven {
        url('https://repo.jenkins-ci.org/public/')
    }
}

assets {
    enableDigests = false
    enableGzip = false
    enableSourceMaps = false
    verbose = false
    configOptions = [
            handlebars: [
                    templateRoot: 'templates'
            ]
    ]
}

configurations {
    webjars {
        transitive = false
    }
}

dependencies {
    webjars 'org.webjars:bootstrap:3.3.4'
    webjars 'org.webjars:jquery:1.11.1'
    webjars 'org.webjars:jquery-ui:1.11.3'
    webjars 'org.webjars:jqueryui-layout:1.4.0'
    webjars 'org.webjars:jstree:3.0.4'
    webjars 'org.webjars:handlebars:4.0.2'
    webjars 'org.webjars.npm:underscore:1.7.0'
    webjars 'org.webjars.npm:backbone:1.2.3'
    webjars 'org.webjars.npm:backbone.marionette:2.4.2'
    webjars 'org.webjars:highlightjs:8.4-4'
}

combineJs {
    source 'build/assets/App.js'
    source 'build/assets'
    include '**/*.js'
    dest = file("${buildDir}/dist/js/app.js")
    dependsOn 'assetCompile'
}

combineCss {
    source 'build/assets'
    include '**/*.css'
    dest = "${buildDir}/dist/css/app.css"
    dependsOn 'assetCompile'
}

task concat {
    dependsOn 'combineJs', 'combineCss'
}

task build {
    dependsOn 'concat', 'processUpdateCenter', 'copyData', 'copyDependencies'
}

githubPages {
    commitMessage = "updated gh-pages for $version"
    repoUri = "git@github.com:$githubUser/job-dsl-plugin.git"
    pages {
        from '.'
        include 'index.html'
        include 'build/data/**'
        include 'build/dist/**'
    }
}

task copyData(type: Copy) {
    from tasks.getByPath(':job-dsl-core:generateApiDoc')
    into "${project.buildDir}/data"
}

task copyDependencies(type: Copy) {
    into file("${buildDir}/dist/vendor")

    configurations.webjars.files.each { File jar ->
        from zipTree(jar)
    }

    include '**/fonts/*'

    include '**/bootstrap.min.css'
    include '**/style.min.css'
    include '**/default.min.css'

    include '**/jquery.min.js'
    include '**/jquery-ui.min.js'
    include '**/jquery.layout.js'
    include '**/jstree.min.js'
    include '**/handlebars.runtime.min.js'
    include '**/underscore-min.js'
    include '**/backbone-min.js'
    include '**/backbone.marionette.min.js'
    include '**/bootstrap.min.js'
    include '**/highlight.min.js'

    exclude '**/core/backbone.marionette.min.js'

    includeEmptyDirs false

    eachFile { details ->
        details.path -= 'META-INF/resources/webjars'
    }
}

task downloadUpdateCenter(type: Download) {
    src 'https://updates.jenkins-ci.org/update-center.json'
    dest 'build/download/update-center.jsonp'
    onlyIfNewer true
}

task processUpdateCenter {
    ext.jsonpFile = file('build/download/update-center.jsonp')
    ext.jsonFile = file('build/data/update-center.json')

    dependsOn tasks.downloadUpdateCenter
    inputs.file jsonpFile
    outputs.file jsonFile

    doLast {
        def lines = jsonpFile.readLines('UTF-8')
        jsonFile.setText(lines[1..-2].join('\n'), 'UTF-8')
    }
}

def tokenizedVersion = version.split(/\.|-/) // version can be either x.y or x.y-SNAPSHOT
int latestVersion = (tokenizedVersion[1] as int) - 1 // extract minor version and decrease by 1 to get the latest released version
(38..latestVersion).each { minorVersion ->
    def version = "1.${minorVersion}"
    Configuration config = configurations.create("api-${version}")
    dependencies.add(config.name, "org.jenkins-ci.plugins:job-dsl-core:$version:apidoc@json")
    tasks.copyData.from(config)
}

tasks.publishGhPages.dependsOn('build')

task clean(type: Delete) {
    delete project.buildDir
}
